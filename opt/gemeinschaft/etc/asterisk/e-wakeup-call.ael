//---------------------------------------------------------------------
//  Wakeup-Call Macros
//---------------------------------------------------------------------

macro wakeuptime-announce (hour,minute) {

	Wait(1);
	if( "${CHANNEL(language)}" == "en" ) {
		Playback(/opt/gemeinschaft/sounds/${lang}/wakeup_call_confirmed);
		//Playback(digits/at);
		SayNumber(${hour});
		if( "${minute}" == "0" ) {
			Playback(digits/oclock);
		}
		else {
			SayNumber(${minute});
		}
	}
	else {
		Playback(/opt/gemeinschaft/sounds/${lang}/wakeup_call_confirmed);
		SayNumber(${hour});
		Playback(digits/oclock);
		if( "${minute}" != "0" ) {
			SayNumber(${minute});
		}
	}

	return;	

};

macro wakeuptime-remove(user) {
	AGI(/opt/gemeinschaft/dialplan-scripts/wakeup-call-delete.agi,${user});
	if ( "${ret}" == "" || "${ret}" == "error" ) {
		Playback(/opt/gemeinschaft/sounds/${lang}/error_wakeup_call_cancelled);
		Wait(0.5);
		Playback(vm-goodbye);
		Hangup();
	}

	Playback(/opt/gemeinschaft/sounds/${lang}/wakeup_call_cancelled);
	Wait(0.5);
	Playback(vm-goodbye);
	Hangup();

	return;
	
};

//---------------------------------------------------------------------
//  Wakeup-Call
//---------------------------------------------------------------------

context wakeup-call {

	s => {
		AGI(/opt/gemeinschaft/dialplan-scripts/get-user-language,${user_name});
		Set(__lang=${gs_userlang_gs});
		&caller-count();

		AGI(/opt/gemeinschaft/dialplan-scripts/check-group-permission.agi,${user_id},${user_name},wakeup_call);
		if ("${permission}" != "true") {
			Verbose(1,### ${user_name} have no wakeup_call permission);
			Hangup();
		}

		AGI(/opt/gemeinschaft/dialplan-scripts/wakeup-call-check.agi,${user_name});
		
		if ( "${ret}" == "" || "${ret}" == "error" )
			goto error;
		else if( "${ret}" == "notset" )
			goto notset;
			
			Answer();
			&wakeuptime-announce(${hour},${minute});
			jump s@wakeup-call-set;
			

		notset:
			Answer();
			Wait(1);
			Playback(/opt/gemeinschaft/sounds/${lang}/no_wakeup_call);
			
			jump s@wakeup-call-notset;
		error:
			Wait(1);
			Playback(/opt/gemeinschaft/sounds/${lang}/error);
			Wait(0.5);
			Playback(vm-goodbye);
			Hangup();
	};

};

context wakeup-call-set {
	
	s => {
		Answer();
	begin:
		Background(/opt/gemeinschaft/sounds/${lang}/alarm_change);
		WaitExten(20);
	};
	#=> {
		&wakeuptime-remove(${user_name});
	};
	*=> {
		goto wakeup-call-notset|s|1;
	};
	t => {
		jump h;
	};
	i => {
		jump h;
	};
	h => {
		Hangup();
	};
};

context wakeup-call-notset {

	s => {
		Read(wtime,/opt/gemeinschaft/sounds/${lang}/wakeup_call_request,4,,,5);

		if ( "${READSTATUS}" == "OK" ) {

			AGI(/opt/gemeinschaft/dialplan-scripts/wakeup-call-set.agi,${user_name},${wtime});
			if ( "${isset}" == "error" ) {
				Playback(/opt/gemeinschaft/sounds/${lang}/error_entry);
				goto wakeup-call-notset|s|1;
			}
			else {
				Playback(/opt/gemeinschaft/sounds/${lang}/wakeup_call_saved);
			}			

		}
		else {
			Playback(/opt/gemeinschaft/sounds/${lang}/error_entry);
			goto wakeup-call-notset|s|1;
		}
		

		AGI(/opt/gemeinschaft/dialplan-scripts/wakeup-call-check.agi,${user_name});
		if ( "${ret}" == "" || "${ret}" == "error" ) {
			Hangup();
		}
		else if( "${ret}" == "set" ) {
			&wakeuptime-announce(${hour},${minute});
		}
		else {
			Wait(0.5);
			Playback(/opt/gemeinschaft/sounds/${lang}/no_wakeup_call);
		}
		Wait(0.5);
		Playback(vm-goodbye);
		Hangup();
		

	};


};


context wakeup-job {

	s => {
		AGI(/opt/gemeinschaft/dialplan-scripts/get-gs-language-by-ast-code,${CHANNEL(language)});
		Answer();
		Wait(0.5);
		Playback(/opt/gemeinschaft/sounds/${gs_userlang_gs}/wakeup_call_announcement);
		SayUnixTime(,,R);
		Wait(0.5);
		MusicOnHold();
	};

};
